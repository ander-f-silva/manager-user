plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'org.jetbrains.kotlin.jvm' version '1.2.71'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.2.71'
	id 'com.github.ben-manes.versions' version '0.21.0'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

group = 'br.com.pp'
version = '1.0.0'
sourceCompatibility = '1.8'

repositories {
	jcenter()
	mavenCentral()
	maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

jacoco {
	toolVersion = '0.8.2'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'io.springfox:springfox-swagger2:3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0-SNAPSHOT'
	implementation 'io.springfox:springfox-spring-webflux:3.0.0-SNAPSHOT'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.2'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
	testCompile 'org.mockito:mockito-core:2.+'
	testCompile 'org.mockito:mockito-junit-jupiter:2.18.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
	testImplementation 'io.projectreactor:reactor-test'
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

test {
	useJUnitPlatform()
	systemProperty 'junit.jupiter.extensions.autodetection.enabled','true'
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacocoHtml")
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.95
			}
			excludes = [
					'br.com.pp.user.UserApplication',
					'br.com.pp.user.dto.*',
					'br.com.pp.user.exception.*'
			]
		}
		rule {
			element = 'CLASS'
			excludes = [
					'br.com.pp.user.UserApplication',
					'br.com.pp.user.dto.*',
					'br.com.pp.user.exception.*'
			]
			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.95
			}
		}
		rule {
			element = 'CLASS'
			excludes = [
					'br.com.pp.user.UserApplication',
					'br.com.pp.user.dto.*',
					'br.com.pp.user.exception.*'
			]
			limit {
				counter = 'METHOD'
				value = 'COVEREDRATIO'
				minimum = 0.95
			}
		}
	}
}

build.finalizedBy jacocoTestReport
